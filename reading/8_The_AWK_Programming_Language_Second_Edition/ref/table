# table - simple table formatter

BEGIN {
    FS = "\t"; blanks = sprintf("%100s", " ")
    num_re = "^[+-]?([0-9]+[.]?[0-9]*|[.][0-9]+)$"
}
{   row[NR] = $0
    for (i = 1; i <= NF; i++) {
        if ($i ~ num_re)
            nwid[i] = max(nwid[i], length($i))
        wid[i] = max(wid[i], length($i))
    }
}
END {
    for (r = 1; r <= NR; r++) {
        n = split(row[r], d)
        for (i = 1; i <= n; i++) {
            sep = (i < n) ? "   " : "\n"
            if (d[i] ~ num_re)
                printf("%*s%s", wid[i], numjust(i,d[i]), sep)
            else
                printf("%-*s%s", wid[i], d[i], sep)
        }
    }
}

function max(x, y) { return (x > y) ? x : y }

function numjust(n, s) {   # position s in field n
    return s substr(blanks, 1, int((wid[n]-nwid[n])/2))
}
